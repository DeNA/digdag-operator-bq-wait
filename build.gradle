plugins {
    id 'java-library'
    id 'eclipse'
    id 'com.diffplug.gradle.spotless' version '3.27.1'
    id 'com.github.spotbugs' version '3.0.0'
    id 'groovy' // For Spock
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group = 'com.dena.digdag'
version = '0.1.0'

def jdkVersion = '1.8'
sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion

def digdagVersion = '0.10.5'

wrapper {
    // Find the latest version at https://gradle.org/releases/
    gradleVersion = '6.1'
}

def _licenseHeader = '''\
/*
* SPDX-FileCopyrightText: 2020 DeNA Co., Ltd.
* SPDX-License-Identifier: Apache-2.0
*
* Copyright (c) 2020 DeNA Co., Ltd.
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*        http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
'''

spotless {
    java {
        licenseHeader _licenseHeader
        removeUnusedImports()
        eclipse().configFile 'eclipse-formatter.xml'
    }
}

spotbugs {
  toolVersion = '3.1.12'
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports.xml.enabled = false
  reports.html.enabled = true
}

repositories {
    mavenCentral()
}

shadowJar {
    classifier = null // ShadowJar automatically put 'all' classifier. So I turn it off.
    dependencies {
        exclude(dependency('io.digdag:.*'))
    }
}

def github_repo = System.getenv 'github_repo'
def github_user = System.getenv 'github_user'
def github_token = System.getenv 'github_token'

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }

    repositories {
        maven {
            name = 'local'
            url "$buildDir/repo"
        }

        maven {
            name = 'github_packages'
            url 'https://maven.pkg.github.com/' + github_repo
            credentials {
                username = github_user
                password = github_token
            }
        }
    }
}

dependencies {
    implementation group: 'io.digdag', name: 'digdag-standards', version: digdagVersion
    implementation group: 'io.digdag', name: 'digdag-spi', version: digdagVersion
    implementation group: 'io.digdag', name: 'digdag-plugin-utils', version: digdagVersion
    implementation 'com.google.cloud:google-cloud-bigquery:1.105.0'

    // Use the latest Groovy version for Spock testing
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.8'
    // Use the awesome Spock testing and specification framework even with Java
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'junit:junit:4.12'
    // Use digdag test utils
    testImplementation group: 'io.digdag', name: 'digdag-core', version: digdagVersion
    testImplementation group: 'io.digdag', name: 'digdag-core', version: digdagVersion, classifier: 'tests'
    testImplementation group: 'io.digdag', name: 'digdag-client', version: digdagVersion, classifier: 'tests'
}
